plugins {
	alias(libs.plugins.fabric.loom)
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_21

archivesBaseName = project.archives_base_name
version = libs.versions.mod.version.get()
group = project.maven_group

repositories {
	mavenCentral()
	maven { url "https://maven.meteordev.org/releases" }
	maven { url "https://maven.meteordev.org/snapshots" }
	maven { url "https://maven.seedfinding.com/" }
	maven { url "https://maven-snapshots.seedfinding.com/" }
	maven { url 'https://jitpack.io' }
	maven { url 'https://maven.duti.dev/releases' }
}

loom {
	accessWidenerPath = file("src/main/resources/meteor-rejects.accesswidener")
}

configurations {
	// configuration that holds jars to include in the jar
	extraLibs
}

dependencies {
	// Minecraft
	minecraft libs.minecraft
	mappings variantOf(libs.yarn) { classifier("v2") }
	modImplementation libs.fabric.loader

	// Meteor Client
	modImplementation libs.meteor.client
	modCompileOnly libs.baritone

	// Cubiomes - multi-platform support
	extraLibs(libs.cubiomes.main) { transitive = false }
	extraLibs(variantOf(libs.cubiomes.linux64) { classifier("linux64") }) { transitive = false }
	extraLibs(variantOf(libs.cubiomes.osx) { classifier("osx") }) { transitive = false }
	extraLibs(variantOf(libs.cubiomes.windows64) { classifier("windows64") }) { transitive = false }

	// SeedFinding libraries for .locate and OreSim
	extraLibs(libs.mc.math) { transitive = false }
	extraLibs(libs.mc.seed) { transitive = false }
	extraLibs(libs.mc.core) { transitive = false }
	extraLibs(libs.mc.noise) { transitive = false }
	extraLibs(libs.mc.biome) { transitive = false }
	extraLibs(libs.mc.terrain) { transitive = false }
	extraLibs(libs.mc.feature) { transitive = false }

	// SeedcrackerX API
	implementation(libs.seedcrackerx.api) { transitive = false }
	include(libs.seedcrackerx.api)

	configurations.implementation.extendsFrom(configurations.extraLibs)
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version,
				"mc_version": libs.versions.minecraft.get(),
				"gh_hash": (System.getenv("GITHUB_SHA") ?: "")
	}
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}
	from {
		configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
	}
}
tasks.withType(Jar) {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"

	// The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
	// JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
	// We'll use that if it's available, but otherwise we'll use the older option.
	def targetVersion = 21
	if (JavaVersion.current().isJava9Compatible()) {
		 it.options.release = targetVersion
	}
}
